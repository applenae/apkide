plugins {
    id 'com.android.application'
}

android {
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            if (variant.buildType.name.equalsIgnoreCase("release")) {
                def appFileName = "APK-IDE-" + variant.versionName + ".apk"
                outputFileName = appFileName
            }
        }
    }
    signingConfigs {
        testKey {
            storeFile file('testkey.jks')
            storePassword 'testkey'
            keyAlias 'testkey'
            keyPassword 'testkey'
        }
    }
    namespace 'com.apkide.ui'
    compileSdk 34

    defaultConfig {
        applicationId 'com.apkide.ui'
        minSdk 26
        targetSdk 34
        versionCode 20230315
        versionName '1.0beta1'
        multiDexEnabled true
        signingConfig signingConfigs.testKey
        renderscriptTargetApi 21
        renderscriptSupportModeEnabled true
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src/main/java', 'src/main/resources']
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    packagingOptions {
        exclude 'androidsupportmultidexversion.txt'
        exclude 'META-INF/**.version'
        exclude 'org/antlr/**'
        exclude 'r8-version.properties'
        exclude 'smali.properties'
        exclude 'baksmali.properties'
        exclude 'resources/**.ser'
        jniLibs {
            useLegacyPackaging true
        }
    }

    buildTypes {
        debug {
            pseudoLocalesEnabled true
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
            shrinkResources false
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.testKey
        }

        release {
            pseudoLocalesEnabled true
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
            shrinkResources true
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.testKey
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    coreLibraryDesugaring libs.desugar.jdk.libs.nio
    implementation libs.androidx.multidex
    implementation libs.androidx.preference
    implementation libs.material

    implementation libs.okbinder2
    annotationProcessor libs.okbinder2.compiler

    implementation project(':analytics')
    implementation project(':common')

    runtimeOnly project(':apktool:core')
    implementation project(':apktool:service')

    implementation project(':language-server:service')
    implementation project(':language-server:api')
    implementation project(':language-server:impl-java')
    implementation project(':language-server:impl-smali')
    implementation project(':language-server:impl-xml')

    implementation project(':smali')
    implementation project(':java-decompiler')
}
